#
# qmake configuration for the Freescale iMX53 board
#
# This mkspec is based and tested on the Yocto Project's Poky Distribution
# with libEGL.so from Freescale without the X11 dependency.
#
# This mkspec is tested with a framebuffer (eglfs) configuration
#
# A typical configure line looks like:
#
# export PATH=/opt/freescale/usr/local/gcc-4.4.4-glibc-2.11.1-multilib-1.0/arm-fsl-linux-gnueabi/bin:/opt/ltib/bin:/opt/freescale/ltib/usr/spoof:/opt/ltib/bin:/opt/freescale/ltib/usr/bin:/usr/local/CodeSourcery/Sourcery_CodeBench_Lite_for_ARM_GNU_Linux/bin:/usr/local/bin:/usr/bin:/bin:/usr/bin/X11:/usr/X11R6/bin
#
# ./configure \
#    -prefix /usr/local/qt-5.2.0 \
#    -hostprefix /usr/local/qt-5.2.0-arm51-fsl-linux-gnueabi \
#    -release -device linux-imx51-g++ \
#    -opensource -confirm-license \
#    -device-option CROSS_COMPILE=arm-fsl-linux-gnueabi- \
#    -sysroot /opt/ltib/rootfs \
#    -no-gif -no-libjpeg -no-openssl -no-iconv -no-pch -no-xcb -no-dbus -no-wayland -no-cups -no-gtkstyle \
#    -opengl es2 -eglfs \
#    -make libs \
#    -v

include(../common/linux_device_pre.conf)

QMAKE_INCDIR            += $$[QT_SYSROOT]/usr/include
QMAKE_LIBDIR            += $$[QT_SYSROOT]/usr/lib

QMAKE_LIBS_EGL          += -lEGL
QMAKE_LIBS_OPENGL       = -lGLU -lGL
QMAKE_LIBS_OPENGL_QT    = -lGL
QMAKE_LIBS_OPENGL_ES1   = -lGLESv1_CM
QMAKE_LIBS_OPENGL_ES1CL = -lGLES_CL
QMAKE_LIBS_OPENGL_ES2   += -lGLESv2 -lEGL
QMAKE_LIBS_OPENVG       += -lOpenVG -lEGL

SYSROOT = /opt/ltib/rootfs

QMAKE_INCDIR_OPENGL       = $$SYSROOT/usr/include/GL
QMAKE_LIBDIR_OPENGL       = $$SYSROOT/usr/lib
QMAKE_INCDIR_POWERVR      = $$SYSROOT/usr/include
QMAKE_INCDIR_OPENGL_ES1   = $$SYSROOT/usr/include/GLES
QMAKE_LIBDIR_OPENGL_ES1   = $$SYSROOT/usr/lib
QMAKE_INCDIR_OPENGL_ES1CL = $$SYSROOT/usr/include/GLES
QMAKE_LIBDIR_OPENGL_ES1CL = $$SYSROOT/usr/lib
QMAKE_INCDIR_OPENGL_ES2   = $$SYSROOT/usr/include/GLES2
QMAKE_LIBDIR_OPENGL_ES2   = $$SYSROOT/usr/lib
QMAKE_INCDIR_EGL          = $$SYSROOT/usr/include/EGL
QMAKE_LIBDIR_EGL          = $$SYSROOT/usr/lib
QMAKE_INCDIR_OPENVG       = $$SYSROOT/usr/include/VG
QMAKE_LIBDIR_OPENVG       = $$SYSROOT/usr/lib

QMAKE_LFLAGS            += -Wl,-rpath-link,$$[QT_SYSROOT]/usr/lib

IMX5_CFLAGS             = -march=armv7-a -mfpu=vfpv3 -mfloat-abi=softfp -DLINUX=1 -DEGL_API_FB=1 -DQT_COORD_TYPE=float -Wno-psabi
IMX5_CFLAGS_RELEASE     = -O3 $$IMX5_CFLAGS

QMAKE_CFLAGS_RELEASE    += $$IMX5_CFLAGS_RELEASE
QMAKE_CXXFLAGS_RELEASE  += $$IMX5_CFLAGS_RELEASE
QMAKE_CFLAGS_DEBUG      += $$IMX5_CFLAGS
QMAKE_CXXFLAGS_DEBUG    += $$IMX5_CFLAGS



include(../common/linux_device_post.conf)

load(qt_config)

